macro-def (CommitOrder_Section) {
	params {
		param (commitOrder) {
			type (CommitOrder)
		}
	}
	content {
		if (size(commitOrder.order.items) == 0) {
			section {
				content {
					single-line {
						text {
							value ("Shopping cart is empty!")
							style (Title_M)
						}
					}
				}
			}
		} else {
			section {
				content {
					for-each (commitOrder.order.items) {
						as (item) {
							image-card {
								aspect-ratio (4:3)
								image-url ("[#{value(item.shirt.images[0].url)}]")
								title-area {
									halign (Start)
									slot1 {
										text {
											value ("")
											style (Title_M)
										}
									}
								}
							}
							title-area {
								halign (Start)
								slot1 {
									text {
										value ("#{value(item.shirt.title)}")
										style (Title_M)
									}
								}
								slot2 {
									single-line {
										text {
											value ("#{value(item.shirt.price)}")
											style (Detail_L_Soft)
										}
									}
								}
							}
							split-input-cell {
								left {
									input-cell {
										label ("Quantity")
										value ("#{value(item.quantity)}")
										on-click {
											//This intent relies on searchTerm matching the item which is not a good practice, a better approach
											//was to allow an ID as input to SelectItem and use `this.id` in the intent
											intent {
												goal {
													CommitOrder
													@context (Continuation) { Order }
												}
												value { SearchTerm$expr(item.shirt.title) }
												route { UpdateOrder }
												route { GetQuantity }
											}
										}
									}
								}
								right {
									input-cell {
										label ("Size")
										value ("#{value(item.size)}")
										on-click {
											// This intent relies on searchTerm matching the item which is not a good practice, a better approach
											// was to allow an ID as input to SelectItem and use `this.id` in the intent
											intent {
												goal {
													CommitOrder
													@context (Continuation) { Order }
												}
												value { SearchTerm$expr(item.shirt.title) }
												route { UpdateOrder }
												route { GetSize }
											}
										}
									}
								}
							}
							single-line {
								spacer
							}
						}
					}
				}
			}
		}
	}
}